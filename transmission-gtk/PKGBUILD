# Maintainer : Tom Gundersen <teg@jklm.no>
# Maintainer : Ionut Biru <ibiru@archlinux.org>

pkgbase=transmission
pkgname=transmission-gtk
pkgver=2.94
pkgrel=2
pkgdesc='Fast, easy, and free BitTorrent client (GTK+ GUI)'
arch=(x86_64)
url="http://www.transmissionbt.com/"
license=(MIT)
depends=(curl libevent gtk3 desktop-file-utils hicolor-icon-theme)
optdepends=('notification-daemon: Desktop notification support'
            'transmission-cli: daemon and web support')
makedepends=(gtk3 intltool curl libevent systemd)
source=(https://github.com/transmission/transmission-releases/raw/master/transmission-${pkgver}.tar.xz
        transmission-2.90-libsystemd.patch
        transmission-gtk-width.patch)
sha256sums=('35442cc849f91f8df982c3d0d479d650c6ca19310a994eccdaa79a4af3916b7d'
            '9f8f4bb532e0e46776dbd90e75557364f495ec95896ee35900ea222d69bda411'
            '36e796817b09c82dd0e4ddefff871ea9823d98979b8a4fd64429fe8c4146be73')

prepare() {
  cd $pkgbase-$pkgver
  patch -p1 -i "$srcdir/transmission-2.90-libsystemd.patch"
  patch -p1 -i "$srcdir/transmission-gtk-width.patch"

  rm -f m4/glib-gettext.m4
  autoreconf -fi
}

build() {
  cd $pkgbase-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgbase-$pkgver

  make -C gtk DESTDIR="$pkgdir" install
  make -C po DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/transmission-gtk/COPYING"
}
