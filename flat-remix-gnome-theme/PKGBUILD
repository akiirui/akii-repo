# Contributor: Daniel Ruiz de Alegria <daniel@drasite.com>
# Maintainer: Akatsuki Rui <akiirui@outlook.com>

pkgbase=flat-remix-gnome-theme
pkgname=(flat-remix-gnome-theme
         flat-remix-gnome-theme-dark)
pkgver=20200415
pkgrel=1
pkgdesc="A pretty simple shell theme inspired on material design."
arch=('any')
url="https://drasite.com/flat-remix-gnome"
license=('CC-BY-SA-4.0')
conflicts=(flat-remix-gnome)
_pkgbase=flat-remix-gnome
_commit=5fdb1c1953dc747b90c02e3305f78f60b43549d3
source=("https://github.com/daniruiz/flat-remix-gnome/archive/$_commit/flat-remix-gnome-$_commit.tar.gz")
sha512sums=('03cb883213cbb61fc8c757c55db661a038916ad35fbbd230eae1498fc49347a326e8303aa3df8dda880ed74685c776ecf380d2360f01cd842e2b234c58f239b3')


prepare() {
    cd $_pkgbase-$_commit
    sed -i 's/-arrow-border-width: 12px;/-arrow-border-width: 0;/g' Flat-Remix*/gnome-shell/gnome-shell.css
}

package_flat-remix-gnome-theme() {
    pkgdesc="A pretty simple shell theme inspired on material design."

    cd $_pkgbase-$_commit
    install -d ${pkgdir}/usr/share/themes
    cp -a Flat-Remix            ${pkgdir}/usr/share/themes/
    cp -a Flat-Remix-fullPanel  ${pkgdir}/usr/share/themes/
}

package_flat-remix-gnome-theme-dark() {
    pkgdesc="A pretty simple shell theme inspired on material design. Dark variant."

    cd $_pkgbase-$_commit
    install -d ${pkgdir}/usr/share/themes
    cp -a Flat-Remix-Dark           ${pkgdir}/usr/share/themes/
    cp -a Flat-Remix-Dark-fullPanel ${pkgdir}/usr/share/themes/
}
